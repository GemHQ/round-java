/*
 * This build file was auto generated by running the Gradle 'init' task * by 'Julian' at '6/16/14 5:27 PM' with Gradle 1.12
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/1.12/userguide/tutorial_java_projects.html
 */

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
  }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

allprojects {
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

repositories {
  mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
  compile 'com.squareup.okhttp:okhttp:2.1.0'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'com.google.guava:guava:18.0'
  compile('org.bitcoinj:bitcoinj-core:0.13.1') {
    exclude module: 'scrypt'
  }
  compile 'org.abstractj.kalium:kalium:0.3.0'
  compile 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
  compile 'com.madgag.spongycastle:prov:1.51.0.0'

  testCompile "org.codehaus.groovy:groovy-all:2.4.1"
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
  testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

bintray {
  def localProperties = loadLocalProperties()
  user = localProperties.get('bintray.user')
  key = localProperties.get('bintray.api_key')
  publications = ['GemPublication']
  pkg {
    repo = 'maven'
    name = 'round-java'
    userOrg = 'gem'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/GemHQ/round-java.git'
    version {
      name = '0.8.0'
      vcsTag = '0.8.0'
    }
  }
}

publishing {
  publications {
    GemPublication(MavenPublication) {
      from components.java
      groupId 'co.gem'
      artifactId 'round-java'
      version '0.8.0'
    }
  }
}

def loadLocalProperties() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties
}